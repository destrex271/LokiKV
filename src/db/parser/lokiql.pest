// Literals
WHITESPACE = _{ " " | "\n" | "\r" | "\t" }
SEPARATOR  = _{ ";" }
COMMA      = _{"," | ", " | " ,"}
FLOAT = @{ "-"? ~ ASCII_DIGIT+ ~ "." ~ ASCII_DIGIT* }
INT = @{ "-"? ~ !FLOAT ~ ASCII_DIGIT+ }
BLOB       = @{ "<BLOB_BEGINS>" ~ (!"<BLOB_ENDS>" ~ ANY)* ~ "<BLOB_ENDS>" }
STRING     = @{ "'" ~ INNER ~ "'" }
INNER      = @{ CHAR* }
CHAR       =  {
    !("'" | "\\") ~ ANY
  | "\\" ~ ("'" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
  | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}
BOOL       = @{ "true" | "false" }
LIST       = {"[" ~ (INT | FLOAT | BLOB | STRING | CHAR | BOOL) ~ (COMMA ~ (INT | FLOAT | BLOB | STRING | CHAR | BOOL))* ~ "]"}

// Key should be a string without whitespaces
ID = @{ (!WHITESPACE ~ ANY)+ }

// Command Types
DUO_COMMAND  = @{ "SET" | "ADDHLL"}
UNI_COMMAND  = @{ "GET" | "INCR" | "DECR" | "/c_hcol" | "/c_bcol" | "/c_bcust" | "/selectcol" | "HLLCOUNT" | "PERSIST" | "LOAD_BCUST" | "LOAD_BDEF" | "LOAD_HMAP" | "DELCOL" | "CHECKPOINT" }
SOLO_COMMAND = @{ "DISPLAY" | "/getcur_colname" | "/listcolnames" | "SHUTDOWN" | "DISPLAY_WAL"}

COMMAND = { (DUO_COMMAND ~ ID ~ (STRING | INT | BOOL | FLOAT | BLOB | LIST)) | (UNI_COMMAND ~ ID) | SOLO_COMMAND }

LOKIQL_FILE = _{ SOI ~ COMMAND ~ (SEPARATOR+ ~ COMMAND)* ~ EOI }
